package yal.analyse ;

import java.util.*;
import yal.arbre.* ;
import yal.arbre.instructions.* ;
import yal.tableSymboles.* ;
import yal.arbre.expressions.calculs.* ;
import yal.arbre.expressions.comparaisons.* ;
import yal.arbre.expressions.Constante;
import yal.arbre.expressions.ConstanteEntiere;
import yal.arbre.expressions.Expression;
import yal.arbre.expressions.Variable;

import yal.exceptions.AnalyseSyntaxiqueException;

import java_cup.runtime.*;

action code {: :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;
        lesTerminaux.put(new Integer(CodesLexicaux.EGAL), "=") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non terminée") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PROGRAMME, DEBUT, FIN, ECRIRE, LIRE ;
terminal Symbol    POINTVIRGULE, EGAL, DOUBLEEGAL, PARENTHESEOUVRANTE, PARENTHESEFERMANTE ;
terminal Symbol    PLUS, MOINS, FOIS, DIVISE;
terminal Symbol    SI, ALORS, FINSI, SINON ;
terminal Symbol    TANTQUE, REPETER, FINTANTQUE;
terminal String    IDF, CSTENTIERE, ENTIER ;
terminal String    SUPERIEUR, INFERIEUR;
   
/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST, INST ;
non terminal Expression EXP ;
non terminal LDECL, DECL ;

/* Precedences. */
/* Produisent un shift/reduce conflict => utilisation du precedence left */
precedence left PLUS, MOINS ;
precedence left FOIS, DIVISE ;
precedence left PARENTHESEOUVRANTE, PARENTHESEFERMANTE ;
precedence left SUPERIEUR, INFERIEUR, DOUBLEEGAL ; // plutôt avant ?

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG    ::=     PROGRAMME  IDF  DEBUT  LDECL  LINST:li  FIN
                {: Programme p = new Programme(lileft + 1) ;
                   p.ajouter(li) ;
                   RESULT =  p ; :}
        |
                PROGRAMME  IDF  DEBUT  LINST:li  FIN
                {: Programme p = new Programme(lileft + 1) ;
                   p.ajouter(li) ;
                   RESULT =  p ; :}
                ;

LDECL   ::=     LDECL  DECL | DECL ;

DECL    ::=     ENTIER IDF:id POINTVIRGULE
                {: EntreeVariable e = new EntreeVariable(id, idleft + 1) ;
                   Symbole s = new Symbole() ;
                   TDS.getInstance().ajouter(e,s) ; :}
                ;

LINST   ::=     LINST:li  INST:i
                {: ((BlocDInstructions)li).ajouter(i) ;
                   RESULT = li ; :}
        |
                INST:i
                {: BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                   b.ajouter(i) ;
                   RESULT = b ; :}
                ;

INST    ::=     IDF:id EGAL EXP:e POINTVIRGULE
                {: RESULT = new Affectation(id, e, eleft + 1) ; :}
        |
                LIRE IDF:id POINTVIRGULE
                {: RESULT = new Lire(id, idleft + 1) ; :}
        |
                ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}

                /* Début des conditions */

        |       SI EXP:e ALORS FINSI
                {: RESULT = new Condition(eleft+1); :}

        |       SI EXP:e ALORS LINST:lifst FINSI
                {: RESULT = new Condition(e, lifst, null, eleft+1); :}

        |       SI EXP:e ALORS SINON LINST:lisnd FINSI
                {: RESULT = new Condition(e, null, lisnd, eleft+1); :}

        |       SI EXP:e ALORS LINST:lifst SINON LINST:lisnd FINSI
                {: RESULT = new Condition(e, lifst, lisnd, eleft+1); :}

                /* Boucle */

        |       TANTQUE EXP:e REPETER LINST:li FINTANTQUE
                {: RESULT = new Boucle(e, li, eleft+1); :}
                ;
    
EXP     ::=     CSTENTIERE:c
                {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
        |
                IDF:id
                {: RESULT = new Variable(id, idleft + 1) ; :}
        |
                PARENTHESEOUVRANTE EXP:e PARENTHESEFERMANTE
                {: RESULT = e ; :}
        |
                EXP:e1 PLUS EXP:e2
                {: RESULT = new Addition(e1, e2, e2left + 1) ; :}
        |
                EXP:e1 MOINS EXP:e2
                {: RESULT = new Soustraction(e1, e2, e2left + 1) ; :}
        |
                EXP:e1 FOIS EXP:e2
                {: RESULT = new Multiplication(e1, e2, e2left + 1) ; :}
        |
                EXP:e1 DIVISE EXP:e2
                {: RESULT = new Division(e1, e2, e2left + 1) ; :}
        |
                EXP:e1 SUPERIEUR:op EXP:e2
                {: RESULT = new Comparaison(e1, e2, op ,e2left + 1) ; :}
        |
                EXP:e1 INFERIEUR:op EXP:e2
                {: RESULT = new Comparaison(e1, e2, op, e2left + 1) ; :}
                ;